buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.eclipse.jgit:org.eclipse.jgit:5.10.0.202012080955-r'
    }
}

plugins {
  id 'net.minecrell.licenser' version '0.3'
  id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    mavenCentral()
}


ext {
    TAG = '0.0'
    GIT_INFO = gitInfo(rootProject.file('.'))
}

group = 'net.minecraftforge'
archivesBaseName = 'legacydev'
version = "${project.ext.GIT_INFO.tag}.${project.ext.GIT_INFO.offset}"
targetCompatibility = sourceCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

license {
  header project.file('LICENSE-header.txt')
  include 'net/minecraftforge/legacydev/**/*.java'
  newLine false
}

ext {
    MANIFEST = manifest{
        attributes([
            'Specification-Title': 'accesstransformers',
            'Specification-Vendor': 'Forge Development LLC',
            'Specification-Version': TAG,
            'Implementation-Title': project.name,
            'Implementation-Version': "${version}+${System.getenv("BUILD_NUMBER")?:0}+${GIT_INFO.abbreviatedId}",
            'Implementation-Vendor': 'Forge Development LLC',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            'Git-Commit': GIT_INFO.abbreviatedId,
            'Git-Branch': GIT_INFO.branch
        ] as LinkedHashMap, 'net/minecraftforge/legacydev/')
    }
}

dependencies {
    compile 'net.sf.jopt-simple:jopt-simple:5.0.4'
}

compileJava {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    manifest.from(MANIFEST)
}

task relocateShadowJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "${project.group}.${project.name}.reloc"
}

shadowJar {
    dependsOn(relocateShadowJar)
    classifier 'fatjar'
    manifest.from(MANIFEST)
}

artifacts {
    archives shadowJar
    archives sourcesJar
}

jar {
    manifest.from(MANIFEST)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            artifact shadowJar
            artifact jar
            pom {
                name = project.archivesBaseName
                packaging = 'jar'
                description = 'Minecraft Forge Legacy Dev'
                url = 'https://github.com/MinecraftForge/LegacyDev'

                scm {
                    url = 'https://github.com/MinecraftForge/LegacyDev'
                    connection = 'scm:git:git://github.com/MinecraftForge/LegacyDev.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/LegacyDev.git'
                }

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/LegacyDev/issues'
                }

                developers {
                    developer {
                        id = 'LexManos'
                        name = 'Lex Manos'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (System.env.MAVEN_USER) {
                url "https://maven.minecraftforge.net/releases/"
                authentication {
                    basic(BasicAuthentication)
                }
                credentials {
                    username = System.env.MAVEN_USER ?: 'not'
                    password = System.env.MAVEN_PASSWORD ?: 'set'
                }
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }
}

def gitInfo(dir) {
    String.metaClass.rsplit = { String del, int limit = -1 ->
        def lst = new ArrayList()
        def x = 0, idx
        def tmp = delegate
        while ((idx = tmp.lastIndexOf(del)) != -1 && (limit == -1 || x++ < limit)) {
            lst.add(0, tmp.substring(idx + del.length(), tmp.length()))
            tmp = tmp.substring(0, idx)
        }
        lst.add(0, tmp)
        return lst
    }

    def git = null
    try {
        git = org.eclipse.jgit.api.Git.open(dir)
    } catch (org.eclipse.jgit.errors.RepositoryNotFoundException e) {
        return [
                tag: '0.0',
                offset: '0',
                hash: '00000000',
                branch: 'master',
                commit: '0000000000000000000000',
                abbreviatedId: '00000000'
        ]
    }
    def desc = git.describe().setLong(true).setTags(true).call().rsplit('-', 2)
    def head = git.repository.exactRef('HEAD')
    def longBranch = head.symbolic ? head?.target?.name : null // matches Repository.getFullBranch() but returning null when on a detached HEAD

    def ret = [:]
    ret.tag = desc[0]
    ret.offset = desc[1]
    ret.hash = desc[2]
    ret.branch = longBranch != null ? org.eclipse.jgit.lib.Repository.shortenRefName(longBranch) : null
    ret.commit = org.eclipse.jgit.lib.ObjectId.toString(head.objectId)
    ret.abbreviatedId = head.objectId.abbreviate(8).name()

    return ret
}
